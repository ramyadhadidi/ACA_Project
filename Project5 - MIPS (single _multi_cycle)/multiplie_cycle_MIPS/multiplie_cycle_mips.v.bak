/*Mulitplie Cycle MIPS Processor
Ramyad Hadidi
1st: 20th Nov 2012
*/

`timescale 1ns/1ns

`define DEBUG	// comment this line to disable register and memory content  writing 

`define AND  4'b0000		//ALUop (module) definitions
`define OR 	 4'b0001
`define ADD  4'b0010
`define SUB  4'b0011
`define NOR  4'b0100
`define SLT  4'b0101
`define XOR  4'b0110
`define SLTU 4'b0111
`define LUI  4'b1000
`define PASS 4'b1001


module  multiplie_cycle_mips (
            input clk,
            input reset
        );
		
	reg PCsrc;
	wire [31:0] rel_add_e;
	wire [31:0] inst;
	reg [31:0] IR;
	reg [1:0] REGdst;
	reg SENZE;
	reg [1:0] MemtoReg;	
	reg WRTMem;
	reg WRTrf;
	reg WRTPC;
	reg WRTMAR;
	reg WRTIR;
	reg srcsel;
	wire ALUzero;
	reg [3:0] ALUop;
	reg [4:0] WR_line;
	reg [31:0] WD_line;
	wire [31:0] RD1,RD2;
	reg [31:0] PC;
	reg [31:0] MAR;
	reg [31:0] PC_line;
	reg [31:0] src_line;
	reg ALUAsel;
	reg [1:0] ALUBsel;
	reg[31:0] aluA_line;
	reg [31:0] aluB_line;
	wire [31:0] aluResult;
	
	//===========================================
	//Program counter and memory
	//MAR and PC  and IR registers
	always @(posedge clk) begin
		case ({reset,WRTPC,WRTMAR})
			3'b1xx: begin
					PC<=32'h00000000;
					MAR<=32'h00000000;
					end
			3'b010: PC<=src_line;
			3'b001: MAR<=src_line;
		endcase		
		case ({reset,WRTIR})
			2'b1x: IR<=32'h00000000;
			2'b01: IR<=inst;
		endcase	
	end
	
	//Multiplexer for MAR and PC
	always @(*)	begin
		case(srcsel)
			1'b0: src_line = aluResult;
			1'b1: src_line = {PC[31:28],IR[25:0],2'b00};
		endcase
		case(PCsrc)
			1'b0: PC_line = PC;
			1'b1: PC_line = MAR;
		endcase
	end
	
	//Program and Data Memory
	async_mem mem(
	.clk(clk),
	.write(WRTMem),
	.address(PC_line),
	.write_data(RD2),
	.read_data(inst)
	);
	
	//===========================================
	//Register File and SE/ZE 
	//WR  and WD multiplexers
	always @(*)	begin
		case(REGdst)
			2'b00: WR_line = inst[20:16];
			2'b01: WR_line = inst[15:11];	
			2'b10: WR_line = 5'b11111;
			//2'b11:
		endcase
		case(MemtoReg)
			2'b00: WD_line = inst;
			2'b01: WD_line = aluResult;
			2'b10: WD_line = PC;
		endcase
	end
	//register file
	reg_file rf(
	.clk(clk),
	.write(WRTrf),
	.WR(WR_line),
	.WD(WD_line),
	.RR1(IR[25:21]),
	.RR2(IR[20:16]),
	.RD1(RD1),
	.RD2(RD2)
	);
	//SE/ZE part
	assign rel_add_e=SENZE?{{16{inst[15]}},inst[15:0]}:{16'h0000,inst[15:0]};	
	//===========================================
	//ALU 	
	always @(*) begin
		case (ALUAsel)
			1'b0 : aluA_line=PC;
			1'b1 : aluA_line=RD1;
		endcase		
		case(ALUBsel)
			1'b00 : aluB_line = {{29{1'b0}},3'b100};
			1'b01 : aluB_line = RD2;
			1'b10 : aluB_line = rel_add_e;
			1'b11 : aluB_line = rel_add_e<<2;
		endcase
	end
	my_alu ALU(
	   .aluA(aluA_line),
	   .aluB(aluB_line),
	   .aluOp(ALUop),
	   .aluResult(aluResult),
	   .ALUZero(ALUzero)
	);
	
	//===========================================
	//control unit
	localparam	//opcodes
		R_type	= 6'h00,
		j		= 6'h02,
		jal		= 6'h03,
		beq		= 6'h04,
		bne		= 6'h05,
        lw		= 6'h23,
        sw		= 6'h2b,
		addiu	= 6'h09,
		slti	= 6'h0a,
		sltiu	= 6'h0b,
		andi	= 6'h08,
		ori		= 6'h0d,
		xori	= 6'h0e,
		lui		= 6'h0f;
		
		
		 
	localparam	//func field opcodes
		opjr	= 6'h08,
		opjalr	= 6'h09,
		//opmfhi	= 6'h10,//
		//opmflo 	= 6'h12,//
		//opmult	= 6'h18,//
		//opmultu	= 6'h19,//
		opaddu	= 6'h21,
		opsubu	= 6'h23,
		opand 	= 6'h24,
		opor	= 6'h25,
		opxor	= 6'h26,
		opnor	= 6'h27,
		opslt	= 6'h2a,
		opsltu	= 6'h2b;
	
	reg [3:0] CS,NS;
	localparam  	//states
		FETCH1		= 4'b0000,
		FETCH2		= 4'b0001,
		EXECUTE		= 4'b0010,
		EXE_BEQ_BNE = 4'b0011,
		EXE_LW1		= 4'b0100,
		EXE_LW2		= 4'b0101,
		EXE_SW1		= 4'b0110,
		EXE_SW2		= 4'b0111;
		 
	wire [5:0]OP = IR[31:26];
	wire [5:0]ALU_OP = IR[5:0];
	
	always @(posedge clk) begin
		if (reset)
			CS<=FETCH1;
		else
			CS<=NS;
	end
	
	always @(*) begin		//controller
		WRTMAR=1'b0;	WRTPC=1'b0;		WRTMem=1'b0;	WRTIR=1'b0;		WRTrf=1'b0;	
		REGdst=2'bxx;	MemtoReg=2'bxx;	SENZE=1'bx; 	ALUop=4'hx;		PCsrc=1'b0;
		ALUAsel=1'bx;	ALUBsel=2'bxx;	srcsel=1'b0;		
		
		case (CS)
			FETCH1:	NS = FETCH2;
			FETCH2: begin
					WRTIR=1'b1;
					ALUAsel=1'b0;
					ALUBsel=2'b00;
					WRTPC=1'b1;
					NS = EXECUTE;
					end
			EXECUTE: begin
				case(OP)
					R_type: begin
						REGdst		= 2'b01;
						WRTrf		= 1'b1;
						MemtoReg	= 2'b01;
						ALUAsel		= 1'b1;
						ALUBsel		= 2'b01;
						NS			= FETCH1;
						case (ALU_OP)
							opand  	: ALUop = `AND;
							opor  	: ALUop = `OR ;
							opaddu	: ALUop = `ADD;
							opsubu	: ALUop = `SUB;
							opsltu 	: ALUop = `SLTU;
							opslt	: ALUop = `SLT;
							opnor  	: ALUop = `NOR;
							opxor	: ALUop = `XOR;
							opjr	: begin
										REGdst		= 2'bxx;
										WRTrf		= 1'b0;
										MemtoReg	= 2'bxx;
										ALUBsel		= 2'bxx;
										WRTPC		= 1'b1;
										ALUop		= `PASS;
									  end
							opjalr	: begin
										REGdst		= 2'b10;
										MemtoReg	= 2'b10;
										ALUBsel		= 2'bxx;
										WRTPC		= 1'b1;
										ALUop		= `PASS;
									  end
						endcase
					end
					
					j: begin
						srcsel		= 1'b1;
						WRTPC		= 1'b1;
						NS			= FETCH1;
					end
					
					jal: begin
						srcsel		= 1'b1;
						WRTPC		= 1'b1;
						REGdst		= 2'b10;
						MemtoReg	= 2'b10;
						WRTrf		= 1'b1;
						NS			= FETCH1;
					end
					
					beq: begin
						NS 			= ALUzero?EXE_BEQ_BNE:FETCH1;
						ALUop		= `SUB;
						ALUAsel		= 1'b1;
						ALUBsel		= 2'b01;
					end
					
					bne: begin
						NS 			= ALUzero?FETCH1:EXE_BEQ_BNE;
						ALUop		= `SUB;
						ALUAsel		= 1'b1;
						ALUBsel		= 2'b01;
					end
					
					lw: begin
						NS			= EXE_LW1;
						ALUop		= `ADD;
						SENZE 		= 1'b1;
						ALUAsel		= 1'b1;
						ALUBsel		= 2'b10;
						WRTMAR		= 1'b1;
					end
					
					sw: begin
						NS			= EXE_SW1;
						ALUop		= `ADD;
						SENZE 		= 1'b1;
						ALUAsel		= 1'b1;
						ALUBsel		= 2'b10;
						WRTMAR		= 1'b1;
					end
					
					addiu:	begin
						NS			= FETCH1;
						WRTrf		= 1'b1;
						ALUop		= `ADD;
						SENZE 		= 1'b0;
						ALUAsel		= 1'b1;
						ALUBsel		= 2'b10;
						REGdst		= 2'b00;
						MemtoReg	= 2'b01;
					end
					
					slti:	begin
						NS			= FETCH1;
						WRTrf		= 1'b1;
						ALUop		= `SLT;
						SENZE 		= 1'b1;
						ALUAsel		= 1'b1;
						ALUBsel		= 2'b10;
						REGdst		= 2'b00;
						MemtoReg	= 2'b01;
					end
					
					sltiu:	begin
						NS			= FETCH1;
						WRTrf		= 1'b1;
						ALUop		= `SLTU;
						SENZE 		= 1'b0;
						ALUAsel		= 1'b1;
						ALUBsel		= 2'b10;
						REGdst		= 2'b00;
						MemtoReg	= 2'b01;
					end
					
					andi:	begin
						NS			= FETCH1;
						WRTrf		= 1'b1;
						ALUop		= `AND;
						SENZE 		= 1'b0;
						ALUAsel		= 1'b1;
						ALUBsel		= 2'b10;
						REGdst		= 2'b00;
						MemtoReg	= 2'b01;
					end
					
					ori:	begin
						NS			= FETCH1;
						WRTrf		= 1'b1;
						ALUop		= `OR;
						SENZE 		= 1'b0;
						ALUAsel		= 1'b1;
						ALUBsel		= 2'b10;
						REGdst		= 2'b00;
						MemtoReg	= 2'b01;
					end
					
					xori:	begin
						NS			= FETCH1;
						WRTrf		= 1'b1;
						ALUop		= `XOR;
						SENZE 		= 1'b0;
						ALUAsel		= 1'b1;
						ALUBsel		= 2'b10;
						REGdst		= 2'b00;
						MemtoReg	= 2'b01;
					end
					
					lui:	begin
						NS			= FETCH1;
						WRTrf		= 1'b1;
						ALUop		= `LUI;
						SENZE 		= 1'b0;
						ALUBsel		= 2'b10;
						REGdst		= 2'b00;
						MemtoReg	= 2'b01;
					end
					//more op here
					
				endcase
			end
			
			EXE_BEQ_BNE: begin
					NS			= FETCH1;
					SENZE 		= 1'b1;
					ALUop		= `ADD;
					ALUAsel		= 1'b0;
					ALUBsel		= 2'b11;
					WRTPC		= 1'b1;
			end
			
			EXE_LW1: begin
					NS			= EXE_LW2;
					PCsrc		= 1'b1;
			end
			
			EXE_LW2: begin
					NS			= FETCH1;
					PCsrc		= 1'b1;
					REGdst		= 2'b00;
					MemtoReg	= 2'b00;
					WRTrf		= 1'b1;
			end
			
			EXE_SW1: begin
					NS			= EXE_SW2;
					PCsrc		= 1'b1;
					WRTMem		= 1'b1;
			end
			
			EXE_SW2: begin
					NS			= FETCH1;
					PCsrc		= 1'b1;
					WRTMem		= 1'b1;
			end
			
			//more states here
		endcase
	end
	
endmodule


//-----------------------------------------------------------------------------------------------------------
//-----------------------------------------------------------------------------------------------------------
//asynchronous memory
module async_mem(
	input clk,
	input write,
	input [31:0] address,
	input [31:0] write_data,
	output [31:0] read_data
);


	reg [31:0] mem_data [0:1023];

	assign read_data = mem_data[ address[31:2] ];

	always @(posedge clk)
		if(write) begin
			mem_data[ address[31:2] ] <= #1 write_data;
			
			`ifdef DEBUG
				$display("[%x] = %x", address, write_data);
			`endif
		end

endmodule
//-----------------------------------------------------------------------------------------------------------
//register file

module reg_file(
	input clk,
	input write,
	input [4:0] WR,
	input [31:0] WD,
	input [4:0] RR1,
	input [4:0] RR2,
	output [31:0] RD1,
	output [31:0] RD2
);

	reg [31:0] reg_data [0:31];

	assign RD1 = reg_data[ RR1 ];

	assign RD2 = reg_data[ RR2 ];

	always @(posedge clk) begin
		if(write) begin
			reg_data[ WR ] <= #1 WD;

			`ifdef DEBUG
			if(WR)
				$display("$%0d = %x", WR, WD);
			`endif

		end
		reg_data[0] <= #1 32'h00000000;
	end

endmodule
//-----------------------------------------------------------------------------------------------------------
//my ALU
module my_alu(
   input 			[31:0]	aluA,
   input 			[31:0]	aluB,
   input 			[3:0]	aluOp,
   output	reg 	[31:0]	aluResult,
   output						ALUZero
);
	wire [32:0]aluAs = $signed(aluA);
	wire [32:0]aluBs = $signed(aluB);
	wire [32:0]results = aluBs-aluAs;

   always @(*)
      case(aluOp)
		 `AND : aluResult = aluA & aluB;
		 `OR  : aluResult = aluA | aluB;
         `ADD : aluResult =	aluA + aluB;           
         `SUB : aluResult = aluA + ~aluB + 1'b1;      
         `SLTU: aluResult = (aluA<=aluB)?32'h00000001:32'b0000000;
         `NOR : aluResult = ~(aluA | aluB);
		 `XOR : aluResult = aluA ^ aluB;
		 `SLT : aluResult = (results[32]==1'b0)?32'h00000001:32'b0000000;
		 `LUI : aluResult = {aluB[15:0],{16{1'b0}}};
		 `PASS: aluResult = aluA;
      endcase

   assign ALUZero = !(|aluResult);

endmodule
//-----------------------------------------------------------------------------------------------------------